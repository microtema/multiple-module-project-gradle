buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.unbroken-dome.gradle-plugins:gradle-testsets-plugin:1.0.2'
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

subprojects {

    group = 'de.microtema.gradle'
    version = '1.0-SNAPSHOT'
    description = "The $project.name API for the application"

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'org.unbroken-dome.test-sets'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'utf-8'

    publishing {
        publications {
            "$project.name"(MavenPublication) {
                from components.java
            }
        }
    }

    dependencies {

        compile 'org.apache.commons:commons-lang3:3.9'
        compile 'commons-io:commons-io:2.6'

        testCompile "junit:junit:4.12"
        testCompile("de.7fate:model-builder:2.0.0") {
            transitive = false
        }
    }

    testSets {
        integrationTest
    }

    check.dependsOn integrationTest
    integrationTest.mustRunAfter test

    project.integrationTest {
        outputs.upToDateWhen { false }
    }

    tasks.withType(Test) {
        reports.html.destination = file("${reporting.baseDir}/${name}")
    }

}
